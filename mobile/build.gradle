apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    signingConfigs {
        release {
            keyAlias project.KEYSTORE_KEY_ALIAS
            keyPassword project.KEYSTORE_KEY_PASS
            storeFile file(project.KEYSTORE_PATH)
            storePassword project.KEYSTORE_STORE_PASS
        }
    }

    compileSdkVersion project.COMPILE_SDK
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    defaultConfig {
        applicationId project.PACKAGE_NAME
        minSdkVersion project.MIN_SDK
        targetSdkVersion project.TARGET_SDK
        versionCode project.VERSION_CODE
        versionName project.VERSION
        buildConfigField "String", "OPEN_DATA_URL", "\"" + project.OPEN_DATA_URL + "\""
        resValue "string", "google_maps_api_key", project.GOOGLE_MAPS_API_KEY
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix '.dev'
            versionNameSuffix '-DEV'
            buildConfigField "String", "BACKEND_URL", "\"" + project.BACKEND_DEV_URL + "\""
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "BACKEND_URL", "\"" + project.BACKEND_PROD_URL + "\""
        }
    }
    flavorDimensions "mobile"
    productFlavors {
        mock {
            dimension "mobile"
        }
        full {
            dimension "mobile"
        }
    }
    lintOptions {
        disable 'MissingTranslation'
        checkReleaseBuilds true
        warningsAsErrors true
        abortOnError true
        htmlReport true
    }
}

dependencies {
    repositories {
        mavenCentral()
    }

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':api')
    implementation project(':common')
    wearApp project(':wear')

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.android.support:design'
    implementation 'com.google.android.gms:play-services-wearable:17.0.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.squareup.dagger:dagger:1.2.5'
    implementation 'com.sothree.slidinguppanel:library:3.0.0'
    implementation 'com.astuetz:pagerslidingtabstrip:1.0.1'

    implementation('io.nlopez.smartlocation:library:3.3.3') {
        transitive = false
    }

    implementation platform('com.google.firebase:firebase-bom:26.8.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    annotationProcessor 'com.squareup.dagger:dagger-compiler:1.2.5'
}
