buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    signingConfigs {
        release {
            keyAlias project.KEYSTORE_KEY_ALIAS
            keyPassword project.KEYSTORE_KEY_PASS
            storeFile file(project.KEYSTORE_PATH)
            storePassword project.KEYSTORE_STORE_PASS
        }
    }

    compileSdkVersion project.COMPILE_SDK
    buildToolsVersion project.BUILD_TOOL
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    defaultConfig {
        applicationId project.PACKAGE_NAME
        minSdkVersion project.MIN_SDK
        targetSdkVersion project.TARGET_SDK
        versionCode project.VERSION_CODE
        versionName project.VERSION
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix '.dev'
            versionNameSuffix '-DEV'
            resValue "string", "google_maps_api_key", project.DEBUG_MAP_API_KEY
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "google_maps_api_key", project.RELEASE_MAP_API_KEY
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions "mobile"
    productFlavors {
        mock {
            dimension "mobile"
        }
        full {
            dimension "mobile"
        }
    }
    lintOptions {
        disable 'MissingTranslation'
        checkReleaseBuilds true
        warningsAsErrors true
        abortOnError true
        htmlReport true
    }
}

dependencies {
    repositories {
        mavenCentral()
    }
    annotationProcessor 'com.squareup.dagger:dagger-compiler:' + project.DAGGER_VERSION
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    wearApp project(':wear')
    implementation project(':api')
    implementation project(':common')
    implementation 'com.android.support:appcompat-v7:' + project.SUPPORT_VERSION
    implementation 'com.android.support:support-v4:' + project.SUPPORT_VERSION
    implementation 'com.android.support:design:' + project.SUPPORT_VERSION
    implementation 'com.google.android.gms:play-services-wearable:' + project.PLAY_SERVICES_VERSION
    implementation 'com.google.android.gms:play-services-maps:' + project.PLAY_SERVICES_VERSION
    implementation 'io.reactivex:rxjava:' + project.RXJAVA_VERSION
    implementation 'io.reactivex:rxandroid:' + project.RXANDROID_VERSION
    implementation 'com.squareup.dagger:dagger:' + project.DAGGER_VERSION
    implementation 'com.sothree.slidinguppanel:library:3.0.0'
    implementation 'com.astuetz:pagerslidingtabstrip:1.0.1'
    implementation('io.nlopez.smartlocation:library:3.3.3') {
        transitive = false
    }
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
        transitive = true;
    }
}
